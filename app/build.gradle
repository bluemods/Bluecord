apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'com.discord'
    android.buildFeatures.buildConfig true

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/*.kotlin_module'
    }

    def buildTime = { ->
        return "\"" + new Date().toString() + "\""
    }

    def commitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return "\"" + stdout.toString().trim() + "\""
    }

    defaultConfig {
        applicationId "com.bluecord"
        versionCode 1
        versionName "1.0"

        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 30
        compileSdkVersion 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BUILD_TIME", buildTime()
        buildConfigField "String", "COMMIT_HASH", commitHash()
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        // https://developer.android.com/kotlin/add-kotlin#groovy
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += 'src/main/stubs'
    }

    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding 'utf-8'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation "androidx.preference:preference:1.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation 'com.google.android.material:material:1.12.0'

    // Java 9 backport
    // implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.4'
}